// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zx-nx
    Mux16(a=x, b=false , sel=zx , out=zeroOrx);
    Not16(in=zeroOrx,out=zerox);
    Mux16(a=zeroOrx,b=zerox,sel=nx, out=outx);

    //zy-ny
    Mux16(a=y, b=false , sel=zy , out=zeroOry);
    Not16(in=zeroOry,out=zeroy);
    Mux16(a=zeroOry,b=zeroy,sel=ny, out=outy);
    
    //f
    Add16(a = outx, b = outy, out = xplusy);
    And16(a=outx,b=outy,out=xandy);
    Mux16(a=xandy,b=xplusy,sel=f, out=outf);

    //no
    Not16(in=outf, out=negno);
    Mux16(a=outf, b=negno, sel=no , out=out, out[15]=firstout, out[0..7]=finalLeft, out[8..15]=finalRight);
    
    //zr
    Or8Way(in=finalLeft, out=byteL);
    Or8Way(in=finalRight,out=byteR);
    Or(a=byteL,b=byteR,out=result);
    Not(in=result,out=zr);

    //ng
    And(a=firstout,b=true,out=ng);
}